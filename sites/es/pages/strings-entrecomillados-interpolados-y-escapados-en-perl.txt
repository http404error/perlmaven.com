=title Strings en Perl: entrecomillado, interpolación y escape
=timestamp 2014-11-18T15:45:03
=indexes strings, ', ", \, escape character, interpolation, quote, embedded characters, q, qq
=status show
=original quoted-interpolated-and-escaped-strings-in-perl
=books beginner
=author szabgab
=translator davidegx
=comments_disqus_enable 1

=abstract start

Entender como las cadenas de texto, o strings, funcionan en cualquier lenguaje de
programación es importante, más aún en Perl porque son parte de la esencia del lenguaje.
Especialmente si tienes en cuenta que uno de los retroacronimos de Perl es
<b>Practical Extraction and Reporting Language</b> y para eso necesitas manejar muchos strings.

=abstract end

Los strings pueden ser entrecomillados mediante comillas simples <hl>'</hl> 
o comillas dobles <hl>"</hl>, el comportamiento es diferente entre unas y otras.

<h2>Cadenas de texto encerradas en comillas simples</h2>

Si escribes caracteres entre comillas simples <hl>'</hl>, todos los caracteres excepto
la propia comilla simple <hl>'</hl>, son interpretados tal y como están escritos en el
código.

<code lang="perl">
my $name = 'Foo';
print 'Hola $name, como estas?\n';
</code>

El resultado será:

<code>
Hola $name, como estas?\n
</code>

<h2>Cadenas de texto encerradas en comillas dobles</h2>

Los strings dentro de comillas dobles <hl>"</hl> proporcionan interpolación
(las variables contenidas en el string son remplazadas por su contenido),
y también permiten el uso de secuencias de escape como <hl>\n</hl> por una nueva
línea o <hl>\t</hl> por una tabulación.

<code lang="perl">
my $name = 'Foo';
my $time  = "hoy";
print "Hola $name,\ncomo estas $time?\n";
</code>

El resultado será:

<code>
Hola Foo,
como estas hoy?

</code>

Fíjate que hay un <hl>\n</hl> justo después de la coma y otro al final del string.

Para cadenas de texto sencillas como 'Foo' y "hoy" que non contienen los caracteres
<hl>$</hl>, <hl>@</hl>, o <hl>\</hl> da igual como las entrecomillemos.

Las dos siguientes líneas devolverían el mismo resultado:

<code lang="perl">
$name = 'Foo';
$name = "Foo";
</code>


<h2>Direcciones de correo</h2>

El símbolo <hl>@</hl> es interpolado cuando aparece dentro de comillas dobles, por
lo que escribir una dirección de correo requiere un poco de atención.

Con comillas simples <hl>@</hl> no es interpolado.

Con comillas dobles, este código:

<code lang="perl">
use strict;
use warnings;
my $broken_email  = "foo@bar.com";
</code>

Generará un error: <a href="https://perlmaven.com/global-symbol-requires-explicit-package-name">Global symbol "@bar" requires explicit package name at ... line ... (en)</a>
y un warning:
<b>Possible unintended interpolation of @bar in string at ... line ...</b>

El warning proporciona una buena pista sobre cual es el problema.

Por otro lado, este código con el email entrecomillado con comillas simples funcionará.

<code lang="perl">
use strict;
use warnings;
my $good_email  = 'foo@bar.com';
</code>

¿Que ocurre si quieres usar interpolación de variables escalares pero necesitas incluir símbolos como <hl>@</hl>?

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
my $good_email  = "$name\@bar.com";

print $good_email; # foo@bar.com
</code>

Siempre puedes <b>escapar</b> los caracteres especiales, en este caso el símbolo de la arroba <hl>@</hl>, usando el <b>carácter de escape</b>
que es la contrabarra <hl>\</hl>.

<h2>Incluyendo el símbolo del dolar $ dentro de comillas dobles</h2>

De forma similar se puede incluir el signo <hl>$</hl>:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print "\$name = $name\n";
</code>

Mostrará:

<code>
$name = foo
</code>

<h2>Escapando el carácter de escape</h2>

Hay algunos casos en los que quieres incluir la contrabarra dentro de un string.
Si pones la contrabarra <hl>\</hl> en un string encerrado en comillas dobles,
Perl pensará que quieres escapar el siguiente carácter y lo hará.

No te preocupes, le puedes indicar Perl que lo evite escapando el carácter de escape:

Simplemente añade otra contrabarra delante:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print "\\$name\n";
</code>

<code>
\foo
</code>

Se que esta secuencia de escape es un poco rara, pero así es como funciona en la mayoría de lenguajes.

Si quieres intentar entenderlo mejor, prueba algo como:

<code lang="perl">
print "\\\\n\n\\n\n";
</code>

observa lo que muestra:

<code>
\\n
\n
</code>

y trata de entender como funciona.

<h2>Escapando las comillas dobles</h2>

Hemos visto como puedes entrecomillar variables escalares dentro de comillas dobles y también como puedes escapar
el símbolo del dolar <hl>$</hl>.

También hemos visto como puedes usar el carácter de escape <hl>\</hl> y como lo puedes escapar.

¿Y si quieres mostrar una comilla doble dentro de una cadena delimitada por comillas dobles?


Este código tiene un error sintáctico:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print "El "nombre" es "$name"\n";
</code>

Cuando Perl ve el símbolo de comilla doble justo antes de "nombre" piensa que es el final del string
y se queja de que la palabra <b>nombre</b> es un <a href="https://perlmaven.com/barewords-in-perl">bareword (en)</a>
(texto sin comillas).

Como probablemente has adivinado necesitamos escapar el símbolo <hl>"</hl>:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print "El \"nombre\" es \"$name\"\n";
</code>

Esto mostrará:

<code>
El "nombre" es "foo"
</code>

Funciona, pero es bastante difícil de leer.


<h2>El operador qq</h2>

En estos casos es donde el operador <hl>qq</hl> es útil:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print qq(El "nombre" es "$name"\n);
</code>

Para alguien nuevo, qq() puede parecer una llamada a una función, pero no lo es. <hl>qq</hl> es un operador,
veremos en un segundo que más puede hacer, pero primero explicaré esto.

Remplazamos las comillas dobles <hl>"</hl> que envolvían a la cadena por los paréntesis del operador <hl>qq</hl>.
Esto quiere decir que las comillas dobles ya no son un operador especial en esta cadena, por tanto no necesitamos
escaparlas.
Esto hace el código mucho más legible.
Incluso diría elegante, si no temiese la ira de los programadores Python.

Pero, ¿y si quieres incluir paréntesis en tu string?

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print qq(El (nombre) es "$name"\n);
</code>

No hay problema. Siempre y cuando estén balanceados
(es decir, exista el mismo número de <hl>(</hl> y <hl>)</hl>, y siempre teniendo los paréntesis
de apertura antes que los de cierre correspondientes) Perl puede interpretarlo correctamente.

Supongo que querrás romperlo ahora poniendo un paréntesis de cierre antes que uno de apertura:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print qq(El )nombre( es "$name"\n);
</code>

Perl te dará un error de sintaxis porque "nombre" es un <a href="https://perlmaven.com/barewords-in-perl">bareword (en)</a>.
Perl no puede entenderlo todo.

Podrías, por supuesto, escapar los paréntesis en la cadena <hl>\)</hl> y <hl>\(</hl>, pero estaríamos volviendo a tener código
difícil de leer.
¡No gracias!

¡Existe una forma mucho mejor de hacerlo!

¿Recuerdas que escribí que <hl>qq</hl> es un operador y no una función? Por ello puede hacer algunos trucos.

¿Que pasa si cambiamos los paréntesis por llaves? <hl>{}</hl>:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print qq{El )nombre( es "$name"\n};
</code>

Esto funciona y muestra la cadena como queríamos:

<code>
El )nombre( es "foo"
</code>

(incluso aunque no tengo ni idea porque alguien querría mostrar algo como eso...)

Entonces <a href="http://perl.plover.com/yak/presentation/samples/slide027.html">el tío de la segunda fila</a> levanta su mano
y pregunta ¿que pasaría si el string contiene tanto paréntesis como llaves, <b>y</b> también los quieres sin balancear?

¿Así?

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print qq[El )nombre} es "$name"\n];
</code>

mostrando esto:

<code>
El )nombre} es "foo"
</code>


... tenía que haber un uso para los corchetes, ¿no?


<h2>El operador q</h2>

De forma similar a <hl>qq</hl> también existe el operador <hl>q</hl>.
Este operador también te permite seleccionar los delimitadores de tu string,
pero funciona como las comillas simples <hl>'</hl>: <b>NO</b> interpola las variables.

<code lang="perl">
use strict;
use warnings;
print q[El )nombre} es "$name"\n];
</code>

mostrará:

<code>
El )nombre} es "$name"\n
</code>


