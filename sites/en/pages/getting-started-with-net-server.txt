=title Getting started with Net::Server - building an echo server
=timestamp 2015-04-01T08:10:01
=indexes files
=status show
=books net_server
=author szabgab
=comments_disqus_enable 1

=abstract start

In this article you will learn the basics on building a server using <a href="https://metacpan.org/pod/Net::Server">Net::Server</a>.

Why would you want to use <a href="https://metacpan.org/pod/Net::Server">Net::Server</a>? Just visit that page on MetaCPAN and click on
the <a href="https://metacpan.org/requires/distribution/Net-Server?sort=[[2,1]]">Reverse dependencies</a> list on the right hand side.
These are the modules that depend on Net::Server. I could just point out <a href="https://metacpan.org/release/Starman">Starman</a>
which is probably the best stand-alone PSGI server on the market, but there are tens of other modules and applications depending
on Net::Server. It can provide a good foundation for you too.

=abstract end

<h2>Installing Net::Server</h2>

Start by installing <a href="https://metacpan.org/pod/Net::Server">Net::Server</a>.
I'll have a detailed article or two on installing any CPAN module, though in general you just type <hl>cpan Net::Server</hl>
or <hl>cpanm Net::Server</hl>.

<h2>Skeleton</h2>

Before getting into the more complex examples, let's see the skeleton of the most basic server.

This code goes in the <hl>bin/skeleton.pl</hl>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/../lib";
use SkeletonServer;

SkeletonServer->run(port => 8000);
</code>

First we add the <hl>lib</hl> directory to <hl>@INC</hl>.
There are plenty of <a href="/how-to-add-a-relative-directory-to-inc">other, nicer ways</a> to do this, but using
<hl>FindBin</hl> is simple.

Then we load the server and run it, opening it on port 8000.

This code goes into the <hl>lib/SkeletonServer.pm</hl> file.

<code lang="perl">
package SkeletonServer;
use warnings;
use strict;

use base 'Net::Server';

sub process_request {
    # do your stuff
}

1;
</code>

It subclasses the <hl>Net::Server</hl>. The only thing it needs to implement is the <hl>process_request</hl> method.
Once that's done we are ready. We can launch the server by running:

<hl>perl bin/skeleton_server.pl</hl>

It will print something like this on the console:

<code>
2013/08/06-12:37:08 SkeletonServer (type Net::Server) starting! pid(2685)
Resolved [*]:8000 to [0.0.0.0]:8000, IPv4
Binding to TCP port 8000 on host 0.0.0.0 with IPv4
Group Not Defined.  Defaulting to EGID '1000 4 20 24 46 104 115 120 1000'
User Not Defined.  Defaulting to EUID '1000'
</code>

That's it, you have the skeleton running. You can now stop it by pressing Ctrl-C.


<h2>Simple Echo Server</h2>


We have a new module called <hl>lib/SimpleEchoServer.pm</hl> with the following content:

<code lang="perl">
package SimpleEchoServer;
use warnings;
use strict;

use base 'Net::Server';
my $EOL   = "\015\012";

sub process_request {
    print "Welcome to the Echo server, please type in some text and press enter. Say 'bye' if you want to leave$EOL";

    while( my $line = <STDIN> ) {
        $line =~ s/\r?\n$//;
        print qq(You said "$line"$EOL);
        last if $line eq "bye";
    }
}

1;
</code>

Let's go over this code. Basically every time a client connects to this server, the server will
bind the remote connection to the <hl>process_request</hl> method as if that was just a simple script:

Anything printed to the Standard Output <hl>STDOUT</hl> from this function will arrive
to the client on the other side of the socket.

Anything the client sends to the server, will arrive to the Standard Input <hl>STDIN</hl> of
this server. So you write the <hl>process_request</hl> method just as if you were writing a
command line script. How much easier can this get?

OK, there are two difference.

First, instead of using <hl>chomp</hl> to get rid of the trailing newlines we use a regex.
This is because the actual meaning of <hl>\n</hl> depends on the operating system (Windows vs Unix/Linux)
and we can't be fully sure which kind of newline was sent from the client. So we try to get rid of
all the carriage return <hl>CR</hl> and line feed <hl>LF</hl> characters.
(See the article about <a href="/what-is-a-text-file">text files</a> for a deeper, but still not comprehensive
explanation.)

Second, instead of printing newline using <hl>\n</hl> we use this strange
variable <hl>$EOL</hl> that contains <hl>\015\012</hl>. That's just a carriage return
(decimal 10 octal 015) and a line feed (decimal 10 octal 012). The reason we send this
is because most Internet protocols use this as a newlines.

See the quote from <a href="http://en.wikipedia.org/wiki/Newline">Wikipedia</a>

<i>
Most textual Internet protocols (including HTTP, SMTP, FTP, IRC and many others) mandate the use of ASCII CR+LF
('\r\n', 0x0D 0x0A) on the protocol level, but recommend that tolerant applications recognize lone LF ('\n', 0x0A) as well.
In practice, there are many applications that erroneously use the C newline character '\n' instead.
</i>

In a nutshell we need to be <b>flexible in what we accept and strict in what we send</b>.

BTW Don't ask me why is it written in octal and not hexa in this case. I think it is just
because that's what was in the documentation of <hl>Net::Server</hl>.


<hl>bin/simple_echo_server.pl</hl>:

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/../lib";
use SimpleEchoServer;

SimpleEchoServer->run(port => 8000);
</code>

The only change in the script is that it is now called <hl>bin/simple_echo_server.pl</hl>
for more clarity, and the name of the module we use and run is <hl>SimpleEchoServer</hl>
instead of <hl>SkeletonServer</hl>.

Let's run the script now by typing <hl>perl bin/simple_echo_server.pl</hl> in the console.
We get the following output:

<code>
2013/08/06-12:41:07 SimpleEchoServer (type Net::Server) starting! pid(2764)
Resolved [*]:8000 to [0.0.0.0]:8000, IPv4
Binding to TCP port 8000 on host 0.0.0.0 with IPv4
Group Not Defined.  Defaulting to EGID '1000 4 20 24 46 104 115 120 1000'
User Not Defined.  Defaulting to EUID '1000'
</code>

Now let's switch to another console and type:
<hl>telnet localhost 8000</hl>
This will connect to the server on the same machine and print the following:

<code>
Trying ::1...
Trying 127.0.0.1...
Connected to localhost.
Escape character is '^]'.
Welcome to the Echo server, please type in some text and press enter. Say 'bye' if you want to leave
</code>

The first 4 lines were printed by the <hl>telnet</hl> client, the 5th line was already sent by our server.

Now we can type in text and get responses:

<code>
hello
You said "hello"
world
You said "world"
</code>

When we want to quite we just type in <b>bye</b>. The server will echo it back, but then the
<hl>while</hl> loop terminates because of this line of code: <hl>last if $line eq "bye";</hl>:

<code>
bye
You said "bye"
Connection closed by foreign host.
</code>

The last line was already printed by the telnet client.

Meanwhile in the server console nothing happened. The server still runs. We could now
open another telnet session to the same server.

Instead of that we just press Ctrl-C in the window where the server runs and we will see the following printed,
and get our regular prompt back:

<code>
2013/08/06-12:41:39 Server closing!
... $
</code>

<h2>Multiple concurrent connections</h2>

Let's see what happens if we try to connect to the same server with more than one client at the same time.

We launch the server in one terminal by typing <hl>perl bin/simple_echo_server.pl</hl>.
In another terminal we type <hl>telnet localhost 8000</hl> and we see what we saw earlier. We can type some text and
get response:

<code>
Trying ::1...
Trying 127.0.0.1...
Connected to localhost.
Escape character is '^]'.
Welcome to the Echo server, please type in some text and press enter. Say 'bye' if you want to leave
hello
You said "hello"
</code>

We open a 3rd terminal and type <hl>telnet localhost 8000</hl> there too. We see this:

<code>
Trying ::1...
Trying 127.0.0.1...
Connected to localhost.
Escape character is '^]'.
</code>

Please note, we don't see the welcome text that we saw in the other client.
We try to type in <hl>hi</hl> but no response.

Switching back to terminal 2, we can type again there and get response:

<code>
world
You said "world"
</code>

In terminal 3 still nothing happened.

Then we say goodbye in terminal 2 that will echo and quit as we expect:

<code>
bye
You said "bye"
Connection closed by foreign host.
... $
</code>

If we now switch to terminal 3 we see the following:

<code>
Trying ::1...
Trying 127.0.0.1...
Connected to localhost.
Escape character is '^]'.
hi
Welcome to the Echo server, please type in some text and press enter. Say 'bye' if you want to leave
You said "hi"
</code>

The first <hl>hi</hl>, after the <hl>Escape ...</hl> is what we typed. Once we quite the other client
this started to work, printed out the welcome message and handled the text we typed in.
We can now keep typing here and say "bye" once we want to close the connection.

What we saw is that the server could not handle more than one clients at a time. This might be OK
during development, and initial testing, but in most applications we would want to have a server
that can handle multiple connections.

<hl>Net::Server</hl> solves this in a beautiful way. We just need to edit our server code.
Instead of inheriting from <hl>Net::Server</hl> we will need to inherit from one of the other
servers. We just need to replace <hl>use base 'Net::Server';</hl> by <hl>use base 'Net::Server::PreFork';</hl>.

If we launch the server now, and try to connect using two telnet clients at the same time, it will just work.

<h2>Conclusion</h2>

Writing a simple echo server was very easy and it can already handle multiple connections at the same time.

Let's leave it as it is now, and get back building more complex servers in another article.

