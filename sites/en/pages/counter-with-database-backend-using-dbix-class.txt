=title A command line counter with database back-end using DBIx::Class
=timestamp 2015-06-12T09:10:01
=indexes DBIx::Class, DBIC, SQL::Translator, DBD::SQLite
=status show
=books dbic, counter
=author szabgab
=comments_disqus_enable 1

=abstract start

In this simple example we'll see how to use <a href="https://metacpan.org/pod/DBIx::Class">DBIx::Class</a> (aka. DBIC) to
create and use a database for a simple command-line <a href="https://code-maven.com/counter">counter</a> script.

DBIx::Class is an ORM, an Object Relational Mapper. It tries to bridge the conceptual difference between the representation
of the data in a Relational database where it is stored in tables, and the application where the data is stored in
Objects.

In order to understand it you'll have to understand both objects and relational databases, though you don't necessarily need
to know SQL, the language that is usually used to talk to relational databases. One of the key features of every ORM, and thus DBIC
is that, it eliminates the need to use SQL.  At least in the common cases.

=abstract end

<h2>The end product</h2>

In this example we'll build a command line counter that works like this:

<code>
$ perl dbic_counter.pl
Usage: dbic_counter.pl name

$ perl dbic_counter.pl foo
Creating new counter for foo
1

$ perl dbic_counter.pl foo
2

$ perl dbic_counter.pl bar
Creating new counter for bar
1

$ perl dbic_counter.pl foo
3

$ perl dbic_counter.pl foo
4

$ perl dbic_counter.pl bar
2
</code>

It expects a single parameter on the command line, the name of a counter. Every time we provide a name, the scripts displays the next number for
that counter starting from 1. Apparently I even left in a debugging message showing when each counter is created.

<h2>The code representing the database</h2>

We have 3 files in the example:

The main script called <hl>dbic_counter.pl</hl>, the module representing the whole database (or schema) <hl>lib/MyCounter/Schema.pm</hl>,
and <hl>lib/MyCounter/Schema/Result/Counter.pm</hl> that defines and represents the 'counter' table.

The directory layout looks like this:

<code>
$ tree
.
├── dbic_counter.pl
└── lib
    └── MyCounter
        ├── Schema
        │   └── Result
        │       └── Counter.pm
        └── Schema.pm
</code>

The module representing the whole database (the schema) is quite simple:
It sub-classed <a href="https://metacpan.org/pod/DBIx::Class::Schema">DBIx::Class::Schema</a> and then calls
the <hl>load_namespaces</hl> method on the current packages. (<hl>__PACKAGE__</hl> always contains the current
name-space in it, which is the name declared using the <hl>package</hl> keyword.
See <a href="/packages-modules-and-namespace-in-perl">this article</a> if you need some clarification with the names.)

<hl>lib/MyCounter/Schema.pm</hl>

<code lang="perl">
package MyCounter::Schema;
use strict;
use warnings;
use base qw(DBIx::Class::Schema);

__PACKAGE__->load_namespaces();

1;
</code>

<hl>load_namespaces</hl> will load all the modules under the MyCounter::Schema::* name-space representing all the
tables of the database. In our case there is only one such file.


The code of the <hl>MyCounter::Schema::Result::Counter</hl> module is in <hl>lib/MyCounter/Schema/Result/Counter.pm</hl>.
It subclasses <a href="https://metacpan.org/pod/DBIx::Class::Core">DBIx::Class::Core</a>.

Then we declare the name of the table in the database (<hl>counter</hl>), the names of the columns (<hl>name</hl> and <hl>cnt</hl>).
We also have declare one of the fields as <hl>primary_key</hl>. In many cases a special <hl>id</hl> field is created to be the primary key,
but in our case we expect the <hl>name</hl> to be unique, so we can use that as a primary key.

<code lang="perl">
package MyCounter::Schema::Result::Counter;
use strict;
use warnings;
use base qw(DBIx::Class::Core);

__PACKAGE__->table('counter');
__PACKAGE__->add_columns(qw( name cnt ));
__PACKAGE__->set_primary_key('name');

1;
</code>

That's how we describe the <hl>counter</hl> table.


<h2>The script using the database</h2>

Once we created the above two files representing the database we can see the script using
them.


<code lang="perl">
use strict;
use warnings;
use 5.010;

use lib 'lib';
use MyCounter::Schema;

my $name = shift or die "Usage: $0 name\n";

my $file = 'dbix_counter.db';

my $schema = MyCounter::Schema->connect("dbi:SQLite:$file");

if (not -e $file) {
    $schema->deploy();
}

my $counter = $schema->resultset('Counter')->find($name);
if (not $counter) {
    say "Creating new counter for $name";
    $counter = $schema->resultset('Counter')->create({
        name  => $name,
        cnt   => 0,
    });
}
$counter->cnt( $counter->cnt + 1);
say $counter->cnt;
$counter->update;
</code>

(The only reason we require version 5.010 here is because I am lazy and I wanted to use <hl>say</hl> instead of <hl>print</hl>.)


We start with <hl>use lib 'lib';</hl> to <a href="/how-to-add-a-relative-directory-to-inc">adjust @INC</a> to include
the <hl>lib</hl> subdirectory and then we load <hl>MyCounter::Schema</hl>, the main module of the database. This module
will in turn load the module representing the counter table.

<hl>my $schema = MyCounter::Schema->connect("dbi:SQLite:$file");</hl> connects to the database. It is a bit like
the <hl>connect</hl> method of <a href="https://metacpan.org/pod/DBI">DBI</a>, but it returns an object representing the schema.

The comes the nice part. The <hl>deploy</hl> method of this object will create the actual tables in the database. Based on the
description we provided in the modules describing the database, it will create SQL statements (<hl>CREATE TABLE ...</hl>)
and will create the tables. We call it only if the file did not exist earlier as we don't want to try to create the tables
again and again on every run.

In the next line

<code lang="perl">
my $counter = $schema->resultset('Counter')->find($name);
</code>

<hl>$schema->resultset('Counter')</hl> represents the <hl>counter</hl> table. The <hl>find</hl> method will
search in the table for the given value in the primary key field (which is the <hl>name</hl> field in this case),
and return and object representing the row. 

The block in the <hl>if (not $counter) {</hl> statement is going to be executed once for every counter: The first time we use the counter.
There is the left-over debugging statement declaring we are creating a new counter.

The <hl>create</hl> method on the object representing the <hl>counter</hl> table is basically the same as the <hl>INSERT</hl> SQL statement.
It accepts a hash-reference with the key-value pairs of the field names and their initial values. After inserting the row in the database
it also creates an object representing that row in the database. This object is assigned to <hl>$counter</hl>. This is exactly the
same kind of object as was (or would have been) returned by the <hl>all</hl> method a few lines earlier.

So either way, after the block of the if-statement we have an object in <hl>$counter</hl> representing the row in the database where the
<hl>name</hl> field equals to the value given by the user.

<hl>$counter->cnt( $counter->cnt + 1);</hl> increments the field in the object - not yet changing the database.

<hl>$counter->update;</hl> writes the changes value to the database. Similar to the SQL <hl>UPDATE</hl> statement.

<h2>Prerequisites</h2>

For the above code to work one needs to install <a href="https://metacpan.org/pod/DBIx::Class">DBIx::Class</a>,
<a href="https://metacpan.org/pod/DBD::SQLite">DBD::SQLite</a>, and in order to have the <hl>deploy</hl> method work we also need to install
<a href="https://metacpan.org/pod/SQL::Translator">SQL::Translator</a>

If we don't have SQL::Translator installed, the script will still start to run, but when we encounter the <hl>deploy</hl> call it will
throw an exception:

<code>
## DBIx::Class::Storage::DBI::deployment_statements(): Can't deploy without a ddl_dir or SQL::Translator >= 0.11016
(see DBIx::Class::Optional::Dependencies for details) at dbic_counter.pl line 8
</code>

It will also create an empty file for the database and so if you run the script again (even if you already installed SQL::Translator),
you'll get another exception:

<code>
DBIx::Class::Storage::DBI::_prepare_sth(): DBI Exception: DBD::SQLite::db prepare_cached failed: no such table: counter
[for Statement "SELECT me.name [me.name], me.cnt FROM counter me WHERE ( name =
? )"] at ./dbic_counter.pl [dbic_counter.pl] line 18
</code>

Just remove the database file <hl>dbix_counter.db</hl> manually and run the script again. (after installing SQL::Translator)


<h2>The SQL schema</h2>

If you are comfortable with SQL, you might want to know what is going on behind the scenes.

Adding the following call to the main script:

<code lang="perl">
$schema->create_ddl_dir(['SQLite'],
                        '0.1',
                        './'
                        );
</code>

will create a file called <hl>MyCounter-Schema-0.1-SQLite.sql</hl> that will contain the SQL which can be used
to create the database schema. The content of this file is this:

<code>
-- 
-- Created by SQL::Translator::Producer::SQLite
-- Created on Fri May  9 16:55:54 2014
-- 

BEGIN TRANSACTION;

--
-- Table: counter
--
DROP TABLE counter;

CREATE TABLE counter (
  name  NOT NULL,
  cnt  NOT NULL,
  PRIMARY KEY (name)
);

COMMIT;
</code>

In case you use a different tabase (MySQL, PostgreSQL, etc.) you can ask DBIx::Class to show the respective SQL statements.

<h2>Debugging - SQL statements</h2>

Adding the following line:

<code lang="perl">
$schema->storage->debug(1);
</code>

will tell DBIx::Class to print each SQL statement as it executes them.

If we run the script with a name that already started to count (foo) the output looks like this:
(The number 5 is the regular output of our script)
Here we can see a SELECT statement fetching the fields of table, and then an UPDATE statement
saving the new value.

<code>
SELECT me.name, me.cnt FROM counter me WHERE ( me.name = ? ): 'foo'
5
UPDATE counter SET cnt = ? WHERE ( name = ? ): '5', 'foo'
</code>


If we run the script with a name that we have not used earlier (other) the output is slightly different.
In addition to the SELECT and UPDATE statements we also have an INSERT statement that added the initial
0 value to the database.

<code>
SELECT me.name, me.cnt FROM counter me WHERE ( me.name = ? ): 'other'
Creating new counter for other
INSERT INTO counter ( cnt, name) VALUES ( ?, ? ): '0', 'other'
1
UPDATE counter SET cnt = ? WHERE ( name = ? ): '1', 'other'
</code>


<h2>Conclusion</h2>

That's it for now. We just saw one of the most simple example using DBIx::Class.

