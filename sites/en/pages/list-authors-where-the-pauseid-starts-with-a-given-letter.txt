=title List authors where the PAUSEID starts with a given letter
=timestamp 2018-04-17T12:30:01
=indexes SCO, Template::Toolkit, List::MoreUtils, natatime
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

So far we've copied the <a href="/create-the-sco-look-and-feel">look-and-feel from search.cpan.org</a>,
added a few <a href="/add-another-static-page">static pages</a>, and added the look-and-feel to
<a href="/add-404-not-found-page">the 404 page</a>.

=abstract end

<h2>update the README file</h2>

The next <a href="https://github.com/szabgab/MetaCPAN-SCO/commit/9852bd4188fe9bcff0c8e5b0fd9fee36264d5c2d">commit</a> was just to
improve the content of the <a href="https://github.com/szabgab/MetaCPAN-SCO/blob/9852bd4188fe9bcff0c8e5b0fd9fee36264d5c2d/README">README</a>
file in the root of the project. It is displayed on the main page of the <a href="https://github.com/szabgab/MetaCPAN-SCO">GitHub project</a>
so it should contain information that is useful to potential contributors. I started to describe the development environemt.

<h2>Add main authors page</h2>

The main <a href="http://search.cpan.org/author/">authors</a> page looks like a static page, so
I add that too taking the same steps as for the <a href="/add-another-static-page">feedback page</a>
and arriving to this
<a href="https://github.com/szabgab/MetaCPAN-SCO/commit/d3e1400cb82c88548efe6ecec1d15d797dbc177a">commit</a>.

<h2>list authors where the PAUSEID starts with a given letter</h2>

As I looked at the central authors page at <a href="http://search.cpan.org/author/">http://search.cpan.org/author/</a> and clicked on the first letter
it turned out to load the same URL, but with a the letter passed after the question mark:
<a href="http://search.cpan.org/author/?A">http://search.cpan.org/author/?A</a>.
This is the part that is usually called the <b>QUERY_STRING</b>

That means these pages will be served by the same entry that serves the main authors page. After all, they have the same <hl>path_info</hl>.
Earlier we had this code in the <hl>run</hl> method of
<a href="https://github.com/szabgab/MetaCPAN-SCO/blob/d7c0500cf1f5ea66670a9178be60766a68bc425b/lib/MetaCPAN/SCO.pm">lib/MetaCPAN/SCO.pm</a>:

<code lang="perl">
if ($request->path_info =~ m{^/author/?$}) {
    return template('author');
}
</code>

This is what we are going to change now. (Oh and BTW, the <hl>?</hl> at the end of that regular expression is there so it will match both
<hl>/author</hl> and <hl>/author/</hl>. Nothing to do with he <hl>?</hl> that might be in the URL.)


The <a href="https://metacpan.org/pod/Plack::Request">Plack::Request</a> object we already used to fetch the <hl>path_info</hl> also
provides a method called <hl>query_string</hl> that will return the whole string after the <hl>?</hl> in the URL.
Then, if there was nothing in the  <hl>$query_sting</hl>,  we can return the main page immediately.

<code lang="perl">
if ($request->path_info =~ m{^/author/?$}) {
    my $query_string = $request->query_string;
    return template('author') if not $query_string;
}
</code>

I <a href="/search-cpan-org-authors-and-recent">checked</a> the real search.cpan.org site and found that this URL
will disregard any characters provided after the first character and that it serves the list both if the first character was lower-case
and when it was upper-case. I don't think this is really a good idea, it would be probably better to have pages with distinctive and
unique URLs for each data set. So in the future I thinks any of the pages /authors?ABC,   /authors/?a, /authors/?aXz should redirect to
a page like /authors/A. For now, let's implement what search.cpan.org has.

<h3>Retreive the list of authors</h3>

The next step was to extract the first character, convert it to upper-case, and retreive the list of authors with a PAUSE ID
starting with that charcter.
In an attempt to make the code cleaner, I put the code that retreives from the MetaCPAN API into a subroutine called
<hl>authors_starting_by</hl> that will receive the upper-case latter and return a reference to a list with the necessary
information.

<code lang="perl">
    my $lead = substr $query_string, 0, 1;
    my $authors = authors_starting_by(uc $lead);
</code>

In order to implement the <hl>authors_starting_by</hl> first I had to research the MetaCPAN API a bit. I got a few interesting requests
lined up that I can execute with <hl>curl</hl>:

<code>
    curl http://api.metacpan.org/v0/author/_search?size=10
    curl 'http://api.metacpan.org/v0/author/_search?q=author._id:S*&size=10'
    curl 'http://api.metacpan.org/v0/author/_search?size=10&fields=name'
    curl 'http://api.metacpan.org/v0/author/_search?q=author._id:S*&size=10&fields=name'

    or maybe use fetch to download and keep the full list locally:
    http://api.metacpan.org/v0/author/_search?pretty=true&q=*&size=100000 (from https://github.com/CPAN-API/cpan-api/wiki/API-docs )
</code>


<code lang="perl">
sub authors_starting_by {
   my ($char) = @_;

   my @authors = [];
   if ($char =~ /[A-Z]/) {
       eval {
           my $json = get "http://api.metacpan.org/v0/author/_search?q=author._id:$char*&size=5000&fields=name";
           my $data = from_json $json;
           @authors =
               sort { $a->{id} cmp $b->{id} }
               map { { id => $_->{_id}, name => $_->{fields}{name} } } @{ $data->{hits}{hits} };
           1;
       } or do {
           my $err = $@  // 'Unknown error';
           warn $err if $err;
       };
   }
   return \@authors;
}
</code>

The JSON data returned by the MetaCPAN API server contains a hash. Two levels deep in that hash (both with the key 'hits')
we receive an array of the actual results. From that we extract the '_id' field and the 'name'
field that we requested specifically for us. This is probably far from the optimal way to fetch this data, but this worked,
and at this point I was more interested in getting something to work that to be optimal. If the applications starts to
take shape, I am sure there will be a few other people looking at the code and making suggestions. That one of the advantags
of writing Open Source code. Even if others won't look at it, either because of lack of interest or if you work on a similar
project which is closed source, you can still revisit the code and make improvements.
By that time we'll probably have much better understanding of the API and it will be easier to make improvement.

On the other hand if we try to make this absolutely perfect, we'll be stuck here for quite some time and we'll run out
of enthusiasm, or, if this is a closed source project, we might run out of budget.

So we used the <hl>map</hl> two extract the '_id', and 'name' fields, the latter of which came inside an extra layer of hash
with a key 'fields'. Then we took the resultng list of small hashes and sorted them according to their ID.
That's how they were presented on the original page on search.cpan.org.

The eval-block you see here might be strange.

Then came the question, how to turn this list into groups of 4 as they are represented on <a href="http://search.cpan.org/author/?A">search.cpan.org</a>

Me, being the not sufficiently lazy programmer, decided to convert the array into an array of arrays, where each internal array will contain 4 elements
from the original array. It is quite an interesting problem for what there is an excellent solution in 
<a href="http://metacpan.org/pod/List::MoreUtils">List::MoreUtils</a> called <hl>natatime</hl>.
Given a number n and a list, it returns an iterator. For each call, the iterator will return the next n element from the original list
until the it is exhausted. That's how we convert our array reference <hl>$authors</hl> into the array of arrays called <hl>@table</hl>.

Finally we call the <hl>template() function and pass the <hl>@table</hl> array to the <hl>authors</hl> key.


<h3>The template</h3>

The last thing we need here is to add the appropriate code to the template in
<a href="https://github.com/szabgab/MetaCPAN-SCO/blob/d7c0500cf1f5ea66670a9178be60766a68bc425b/tt/author.tt">tt/author.tt</a>:

<code>
<table width="100%">
<% FOR row IN authors %>
  <tr class="<% IF loop.index % 2 %>s<% ELSE %>r<% END %>">
     <% FOR a IN row %>
        <td>
          <a href="/~<% a.id %>/"><b><% a.id %></b></a><br/><small><% a.name %></small>
        </td>
     <% END %>
  </tr>
<% END %>
</table>
</code>

Template::Toolkit provides the option to iterate over an array using the <hl>FOR ... IN ...</hl>  construct.
In our case the array, or rather the array reference arrives as the value of the <hl>authors</hl> key.
Each element represents a row that we need to display, that's why I called the iterator variable 'row'.
That variable itself contains a reference to an array, so internally we use another <hl>FOR ... IN ...</hl> loop
to iterate over each author (using an unfortunately undescriptive variable called 'a'). Each such variable
is a hash reference representing an author with two fields: <hl>a.id</hl> and <hl>a.name</hl>. We build
the link from that:

<code>
     <a href="/~<% a.id %>/"><b><% a.id %></b></a><br/><small><% a.name %></small>
</code>

One more thing, you might have noticed. The lines displayed by search.cpan.org have alternating color.
The alternation is provided by two class-es: <hl>class="s"</hl> and <hl>class="r"</hl>

That's quite easy to create using Template::Toolkit: Inside every <hl>FOR</hl>-loop we can access
the current index of the loop via the <hl>loop.index</hl> variable provided by TT. We can then compute the
modulus 2 of that number which will be 0 and 1 alternating. Based on that value we can pick which class
to set for the current row:

<code>
  <tr class="<% IF loop.index % 2 %>s<% ELSE %>r<% END %>">
</code> 

That brought us to the next
<a href="https://github.com/szabgab/MetaCPAN-SCO/commit/d7c0500cf1f5ea66670a9178be60766a68bc425b">commit</a>

<code>
$ git add .
$ git commit -m "list authors where the PAUSEID starts with a given letter"
</code>


