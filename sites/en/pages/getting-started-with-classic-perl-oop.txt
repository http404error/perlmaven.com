=title Getting started with Classic Perl OOP
=timestamp 2015-04-19T10:30:01
=indexes OOP, bless, ->
=status show
=books advanced
=author szabgab
=comments_disqus_enable 1

=abstract start

While a lot of people recommend using advanced OOP systems for Perl
such as <a href="/moose">Moose</a> or <a href="/moo">Moo</a>, there are many applications that use the 
classic OOP system of Perl. If you want or need to understand such
applications, you'd better make yourself familiar with the classic OOP
system of Perl.

Besides, both Moose and Moo are built on top
of the classic system, so understanding it is important to gain better
understanding of what the more high-level OOP systems do.

=abstract end

I am sure you have already seen code in Perl that used Object Oriented
modules, but let's have a small example here:

<code lang="perl">
use strict;
use warnings;

my $url = 'https://perlmaven.com/';
use WWW::Mechanize;
my $mech = WWW::Mechanize->new( agent => 'wonderbot 1.01' );
$mech->get( $url );
print $mech->content;
</code>

In this example we load the <hl>WWW::Mechanize</hl> module with the <hl>use</hl>
statement, just as we would do with any other module. In OOP terminology
this module is the <b>class</b>. 
Then we use the single-arrow notation to call the <hl>new</hl> <b>constructor</b>,
passing a pair of values to it. This constructor returns an <b>object</b>, which
is also called an <b>instance</b> of the class. This is assigned to a scalar variable.

Then we call the <hl>get</hl> <b>method</b> on the object passing a parameter to
it (<hl>$url</hl>). Finally we call the <hl>content</hl> method of the object,
this time without passing any parameter.

(In most other programming languages the dot <hl>.</hl> is used to invoke the methods
of an object. In Perl, by the time OOP was added to the language,
the <hl>.</hl> character was already used as the concatenation operator,
so <a href="http://wall.org/~larry">Larry Wall</a> had to chose something else.)

Try to save the above code in a file called <hl>get.pl</hl> and run it
using <hl>perl get.pl</hl>. If you have
<a href="http://metacpan.org/module/WWW::Mechanize">WWW::Mechanize</a>
installed, you will see the content of the home page of the Perl Maven site
zooming by. If you don't have it installed you'll get an error message
<a href="/cant-locate-in-inc">Can't locate WWW/Mechanize.pm in @INC</a>.

Can't locate WWW/Mechanize.pm in @INC (you may need to install the WWW::Mechanize module)

Let's see what is needed to implement the skeleton of the above code.

We go step-by-step, first we create a package, then the constructor and then the other methods.

<h2>Creating a package</h2>

When perl encounters <hl>use WWW::Mechanize</hl>, it searches the directories
listed in <hl>@INC</hl> array. In each one of them it looks for a subdirectory
called <hl>WWW</hl> and inside the subdirectory a file called <hl>Mechanize.pm</hl>

In the same directory where the <hl>get.pl</hl> was saved, we create a subdirectory called <hl>lib</hl>,
and inside that a subdirectory called <hl>WWW</hl>, and inside that directory a file called <hl>Mechanize.pm</hl>.
We have a directory structure like this:

<code>
  ../get.pl
     lib/WWW/Mechanize.pm
</code>

The content of Mechanize.pm will be the following:

<code lang="perl">
package WWW::Mechanize;
1;
</code>

That's a <hl>package declaration</hl> and then the <hl>1;</hl> at the end of the file.
So far this is just a module, it does not have any OOP feature.
Let's try to run the script again: <hl>perl get.pl</hl>. You will get the exact same
behavior as previously. If you had WWW::Mechanize installed, perl found it, if you did
not have it, perl still could not find it. In neither case did perl find the version
of WWW::Mechanize we started to put together. The reason is that the <hl>lib</hl>
directory, where we put the module, is not in the <hl>@INC</hl>. There are 
<a href="/how-to-add-a-relative-directory-to-inc">various</a> ways to
<a href="/how-to-change-inc-to-find-perl-modules-in-non-standard-locations">change @INC</a>.
We'll do the most suitable
to our case and we will run the perl script using <hl>perl -Ilib get.pl</hl>.
The <hl>-I</hl> flag of perl will put the parameter it gets at the beginning of <hl>@INC</hl>.
Once we run the script in the above way we get the following error:

<code>
Can't locate object method "new" via package "WWW::Mechanize" at get.pl line 6.
</code>

This means, perl found our (almost empty) version of <hl>WWW::Mechanize</hl>.
Successfully loaded it, but when it tried to call <hl>->new</hl>, it could
not find the implementation. Which is of course not surprising as we have
not written it yet.

<h2>What is that 1; at the end?</h2>

A little detour, let's change the Mechanize.pm file, removing the 1; from the end
so we have:

<code lang="perl">
package WWW::Mechanize;
</code>

If we run the script now: <hl>perl -Ilib get.pl</hl>, we will get:

<code>
WWW/Mechanize.pm did not return a true value at get.pl line 5.
BEGIN failed--compilation aborted at get.pl line 5.
</code>

Perl needs to have a <a href="/boolean-values-in-perl">boolean true</a> value at the end of each module.
The boring <hl>true</hl> value is <hl>1;</hl>, but I've seen modules with <hl>42;</hl>,
quotes from poetry, and even the string <hl>"false";</hl>. I am sure this
freaks out some unsuspecting readers of that code. In reality all
Perl cares about is that the value will be <a href="/boolean-values-in-perl">boolean true</a> in the Perlish
sense.

<h2>The constructor</h2>

So far we created a module but it is not a class yet.

The next step is adding a constructor that will create
an instance  (or object) of the class. If you come from another language or
if you have seen <a href="/moose">Moose</a> or <a href="/moo">Moo</a> you might be surprised that
we need to create a constructor, but this is how the classic OOP system
of Perl works, and this is what happens in both Moose and Moo under the hood.

Let's change the Mechanize.pm file to have the following code:

<code lang="perl">
package WWW::Mechanize;

sub new {
    my ($class, %params) = @_;

    my $self = {};
    while ( my($key,$value) = each %params ) {
        $self->{$key} = $value;
    }
    bless $self, $class;

    return $self;
}

1;
</code>

The <hl>new</hl> <b>constructor</b> is just a regular subroutine declared with the
<hl>sub</hl> keyword. Nothing is really special about it. Not even its name!
In Perl, any subroutine can act as a constructor, the word <hl>new</hl> is not
reserved, or special in any way, except that it is the most common name for the
constructor.

In Object Oriented programming people usually use the term <hl>method</hl>
for all the actions you can do on the class and on the object. (In our
example that means <hl>get</hl>, <hl>content</hl> and even <hl>new</hl> are
methods.  In Perl there is no special keyword to create a method. Methods
(including the constructor) are declared using the <hl>sub</hl> keyword just
as regular functions are. The difference is how you call them (methods are
always called with the single-arrow notation) and what is their first parameter.
When we call the <hl>new</hl> method on the class-name (in our case WWW::Mechanize), perl
will look for the <hl>new</hl> subroutine in the <hl>WWW::Mechanize</hl> package
and call that function passing all the parameters the user sent and,
the name of the class (<hl>WWW::Mechanize</hl>) as the first parameter.

You can read more about
<a href="/methods-functions-and-subroutines-in-perl">methods, functions and subroutines in Perl</a>.

When we call <hl>WWW::Mechanize->new( agent => 'wonderbot 1.01' );</hl> perl will execute
then <hl>new</hl> functions with 3 parameters: <hl>'WWW::Mechanize', 'agent', 'wonderbot 1.01'</hl>

In the <hl>new</hl> method, we are expecting the first parameter to be the class-name, hence
we put it in a variable called <hl>$class</hl>. For the rest of the parameters we are expecting
a set of key-value pairs and we put them in the <hl>%params</hl> hash.

In the next step we create the skeleton of the <hl>object</hl> and put it in the <hl>$self</hl>
variable. The object is going to be represented as a reference to a hash. It starts out empty: <hl>{}</hl>
but we will fill it soon. Basically the <hl>attributes</hl> (or in other name the <hl>members</hl>)
of the class will be kept in this hash. The <hl>attribute</hl> name will be the key in the hash,
and the attribute value will be, well, the value in the hash.

This is what happens in the next 3 lines: We go over the <hl>%params</hl> hash holding
the parameters we received from the user and assign each key-value pair to the hash
reference we just created.

<code lang="perl">
    while ( my($key,$value) = each %params ) {
        $self->{$key} = $value;
    }
</code>
 
BTW we called the variable <hl>$self</hl>. This variable name is arbitrary, we could have used anything
there. Some people use the name <hl>$this</hl>, <hl>$me</hl>, or even just <hl>$o</hl>,
but the most common name people use to hold the current object inside the implementation is <hl>$self</hl>.

So we have the skeleton of the object in <hl>$self</hl> already filled with values,
but so far it is just a regular hash reference.  Without any special powers.

The next call, <hl>bless $self, $class;</hl> is what turns a mere hash-reference
into an <hl>instance of the class</hl>. A bit later I'll explain what is that magic
and why do we need it.

Once that's done, we return the newly created object.

If we run the script again: <hl>perl -Ilib get.pl</hl>

We get the following error:

<code>
Can't locate object method "get" via package "WWW::Mechanize" at get.pl line 7.
</code>

That looks good. It means, the call to <hl>new</hl> worked and we need to implement <hl>get</hl> now.
Before we do that, let's make another small detour.

<h2>Detour: what does the object look like?</h2>

First, change the script and a <hl>print</hl> statement right after the call to <hl>new</hl>

<code lang="perl">
my $url = 'https://perlmaven.com/';
use WWW::Mechanize;
my $mech = WWW::Mechanize->new( agent => 'wonderbot 1.01' );
print "$mech\n";
</code>

and run the script again: <hl>perl -Ilib get.pl</hl>

<code>
WWW::Mechanize=HASH(0x7ffaf8805480)
</code>

We see that the object is a reference to a hash, but we also see that it is somehow related
to <hl>WWW::Mechanize</hl>.

If you now comment-out the call to <hl>bless</hl> in the Mechanize.pm file, and run the script
again you will see:

<code>
HASH(0x7fab54005480)
</code>

This is just a plain reference to a hash without any relation to <hl>WWW::Mechanize</hl>.

This can be useful when you see something like that printed out by mistake or during a
debugging session, so you can recognize if a reference is a plain reference or if it
was blessed into a class.

<h2>Detour: why do we need to bless the reference?</h2>

Now change the <hl>get.pl</hl> script back so it will call <hl>$mech->get($url)</hl>
after calling <hl>new</hl>, but keep the <hl>bless</hl> commented out and run the script.
The resulting error is:

<code>
Can't call method "get" on unblessed reference at get.pl line 7.
</code>

Now enable the call to <hl>bless</hl> in Mechanize.pm and run the script again. This time the
error will be:

<code>
Can't locate object method "get" via package "WWW::Mechanize" at get.pl line 7.
</code>

In the first case, perl did not know what to do at all. In the second case, when we had the
blessing, perl already knew it needs to look for the <hl>get</hl> method in
the <hl>WWW::Mechanize</hl> class. It just could not find it, because we have not implemented
it yet.

If we have a call <hl>Person->new</hl>, perl will look for a functions called 'new'
in the 'Person' name-space. (or module or package).
If we call <hl>$foo->do_something</hl> then if $foo holds a simple reference, perl does not
know what to do with this code and it gives you the error about unblessed reference.

OTOH if $foo is blessed - if it is connected to a name-space - then perl will look for a 
function called <hl>do_something</hl> in that name-space.

That's what <hl>bless</hl> does. It connects a reference to a name-space.

<h2>Adding a method</h2>

Now that we have an object, we would like to go on and implement the <hl>get</hl> method:

<code lang="perl">
sub get {
    my ($self, $address) = @_;

    require LWP::Simple;
    $self->{content} = LWP::Simple::get($address); 

    return;
}
</code>

The implementation of the <hl>get method</hl> is just a regular subroutine.
When we call <hl>$mech->get($url)</hl> in the script, Perl will look
for the <hl>get</hl> subroutine in <hl>WWW::Mechanize</hl> and will
call it passing all the arguments the user sent (In our case it is the
content of the <hl>$url</hl> variable), and passing the object as the
first parameter. In case of <hl>$mech->get($url)</hl>, perl will call
<hl>get($mech, $url)</hl>.

In the implementation of <hl>get</hl> we copy the first parameter to the <hl>$self</hl>
variable, and the second parameter to the <hl>$address</hl> variable.
(We'd probably use there the name <hl>$url</hl>, but in our example that would just create
confusion with the variable in the script. So for the example let's call it <hl>$address</hl>.)

As mentioned above, the name <hl>$self</hl> is not special in any way, but it is quite
common to use this variable to represent the current object inside the implementation
of the class.
 
Then we use the <a href="http://metacpan.org/module/LWP::Simple">LWP::Simple</a> module,
as a simple way to fetch the web page. It is very simple and there are
<a href="/simple-way-to-fetch-many-web-pages">other and more robust</a> ways to fetch the
content of a web page, but it is good enough for our current purposes.

The call <hl>LWP::Simple::get($address);</hl> will download the page and then we assign it to
a key in the hash representing the object: <hl>$self->{content}</hl>.

Basically this is a new attribute of the object that was added during run time.
As the object is just a hash we can do this. There is no need to declare
the attributes up front.

<h2>Adding a getter</h2>

An <hl>accessor</hl> (or getter) is just another simple subroutine, that receives the
current object as the first (and only) parameter and returns the content of the respective
element of the hash:

<code lang="perl">
sub content {
    my ($self) = @_;

   return $self->{content};
}
</code>

<h2>A little warning</h2>

The object is just a reference to a hash. It is fully exposed to the user as well.
There is nothing in perl that would limit the access of the user to the content
of the hash so a programmer could access the content of the object in the script:

<code lang="perl">
$mech->{content} = "Something else";
</code>

There is nothing we can really do about it, at least not easily, but it is important
we are aware of this possibility.

<h2>The full example</h2>

Finally, let's see the whole <hl>Mechanize.pm</hl> code:

<code lang="perl">
package WWW::Mechanize;

sub new {
    my ($class, %params) = @_;

    my $self = {};
    while ( my($key,$value) = each %params ) {
        $self->{$key} = $value;
    }
    bless $self, $class;

    return $self;
}

sub get {
    my ($self, $url) = @_;

    require LWP::Simple;
    $self->{content} = LWP::Simple::get($url); 

    return;
}

sub content {
    my ($self) = @_;

   return $self->{content};
}

1;
</code>


<h2>Exercise</h2>

Change the implementation of Mechanize.pm to use the <hl>agent</hl>
attribute. You will probably need to replace LWP::Simple
with <a href="/simple-way-to-fetch-many-web-pages">some other</a>, more complex tool.



